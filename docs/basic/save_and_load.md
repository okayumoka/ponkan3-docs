# セーブ＆ロード

<div class="note">
Ponkan3のセーブは、吉里吉里2/KAG3のセーブの仕様を参考にしています。<br>
KAG3がラベルの位置の状態をセーブしていたのに対して、Ponkan3ではセーブマークの位置の状態をセーブします。
</div>

## セーブマーク行の意味

Ponkan3のセーブは、少し複雑な仕様になっています。

Ponkan3でセーブデータを保存しても、*セーブする瞬間の状態は保存されません。*
その瞬間ではなく、*最後にセーブマーク行を通過した時点の状態*が保存されます。

以下に例を示します。

```plain
# ゲーム開始スクリプト
;call file: "script/init_system.pon"

|セーブ場所1
;clear
こんにちは。
;pagebreak
;br

|セーブ場所2
;clear
セーブマークを書くと、その位置でセーブできるようになります。
;pagebreak
;br

|セーブ場所3
;clear
セーブマークの後では、かならずclearを実行してください。
;pagebreak
;br
```

`|セーブ場所1`というような記述がある部分が、セーブされる場所です。

`|`の後ろの部分（セーブ場所1、セーブ場所2、セーブ場所3）は、セーブマークの「見出し」テキストです。
見出しはセーブデータに保存され、セーブ画面でそのまま見出しとして表示されます。
プレイヤーに見えるテキストなので、わかりやすい見出しを付けるようにしましょう。

セーブマークの直後には、必ず`clear`コマンドを書くようにしてください。
これは、Ponkan3のセーブデータにはメッセージ情報が保存されないためです。

## セーブマークを書いてはいけない場所

以下のような場所には、セーブマークを書かないようにしてください。

- マクロの中
- サブルーチンの中
- メッセージが画面に表示されている状態の場所

## 見出しの省略

見出しのテキストは省略することができます。省略した場合、直前のセーブマークの見出しと同じ見出しが使われます。

```plain
# ゲーム開始スクリプト
;call file: "script/init_system.pon"

|ゲーム開始
;clear
こんにちは。
;pagebreak
;br

|
;clear
この位置のセーブ見出しは「ゲーム開始」です。
;pagebreak
;br

|
;clear
ここも「ゲーム開始」です。
;pagebreak
;br

|花子ルート1
;clear
ここは「花子ルート1」です。
;pagebreak
;br
```

## セーブマーク名を明示する

実はここまでのセーブマークはすべて、セーブマーク名を省略した記法でした。
セーブマーク名は見出しテキストとは別のもので、セーブマークを区別するためにつける名前です。

セーブマーク名を省略すると、Ponkan3が自動で `__save_mark_0__` というような名前を割り振ります。
数字の部分はスクリプトファイルの中で連番になります。

セーブマーク名を明示する場合は、以下のように書きます。
`my_save_mark`の部分がセーブマーク名になります。

```plain
|my_save_mark:ゲーム開始
```

通常、セーブマーク名は省略してしまったので問題ありません。
ただし、ゲームを一度リリースした後にアップデートするときは、明示しないといけない場合があります。

## セーブマーク名を省略できないパターン

Ponkan3はセーブデータをロードしたとき、セーブデータに保存されているセーブマーク名を利用して、
スクリプトのどの部分を実行していたかを把握します。

では、もしゲームのバージョンアップで、セーブマークの数が増減したとしたら、どうでしょうか。
例にそって考えていきましょう。

最初のリリース時のスクリプトが、以下のようなものだったとします。

```plain
# 最初のリリース時のスクリプト

|ゲーム開始
;clear
こんにちは。
;pagebreak
;br

|
;clear
また遊んでね。
```

セーブマークは全部で2つあります。
セーブマーク名はすべて省略されているので、順番に`__save_mark_0__`、`__save_mark_1__`となります。

その後、ゲームをアップデートして、メッセージが一つ増えました。

```plain
# アップデート後のスクリプト

|ゲーム開始
;clear
こんにちは。
;pagebreak
;br

|
;clear
プレイしてくれてありがとう。
;pagebreak
;br

|
;clear
また遊んでね。
```

セーブマークが1つ増えて、3つになりました。なので、順番に`__save_mark_0__`、`__save_mark_1__`、`__save_mark_2__`です。

では、もしアップデート前のセーブデータが残っていて、`__save_mark_1__`の場所でセーブされていたとしたら、どうなるでしょうか。
セーブしたときは「また遊んでね。」の直前の位置のセーブだったのに、
アップデートしたら「プレイしてくれてありがとう。」の位置になってしまいました。
これでは、セーブデータをロードされたら、おかしなことになってしまいます。

これを回避するために、セーブマークを追加する場合には、セーブマーク名を明示して書くようにします。

```plain
# アップデート後のスクリプト

|ゲーム開始
;clear
こんにちは。
;pagebreak
;br

|added_save_mark:
;clear
プレイしてくれてありがとう。
;pagebreak
;br

|
;clear
また遊んでね。
```

このようにすれば、アップデートしても正しくセーブ＆ロードできます。
